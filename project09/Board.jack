class Board {
    field Array cells;
    field Array playerSelection;
    // field Array cellCenter; // shared array for getting the center of a cell

    constructor Board new() {
        var Array row;
        var int i;

        let cells = Array.new(9);
        let i = 0;
        while (i < 9) {
            let cells[i] = -1;
            let i = i + 1;
        }
        let playerSelection = Array.new(2);
        let i = 0;
        while (i < 2) {
            let playerSelection[i] = 0;
            let i = i + 1;
        }

        return this;
    }

    method boolean select(int player) {
        var boolean isValid;
        var int selectedCell;

        let isValid = false;
        let selectedCell = (playerSelection[0] * 3) + playerSelection[1];
        if (cells[selectedCell] = -1) {
            let isValid = true;
            let cells[(playerSelection[0] * 3) + playerSelection[1]] = player;
            do drawSelection(player);
        }
        return isValid;
    }

    method void printBoard() {
        do Screen.setColor(true);
        // Vertical lines
        do Screen.drawLine(231, 50, 231, 200);
        do Screen.drawLine(281, 50, 281, 200);
        // Horizontal lines
        do Screen.drawLine(181, 100, 331, 100);
        do Screen.drawLine(181, 150, 331, 150);
        return;
    }

    method void initSelection() {
        var int i;

        let i = 0;
        while (~(cells[i] = -1)) {
            let i = i + 1;
        }

        let playerSelection[0] = i / 3;
        let playerSelection[1] = i - (playerSelection[0] * 3);
        do drawSelectionIndicator(playerSelection[0], playerSelection[1]);
        return;
    }

    method void drawX(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
        do Screen.setColor(true);
        do Screen.drawLine(center[0] - 10, center[1] + 10, center[0] + 10, center[1] - 10);
        do Screen.drawLine(center[0] + 10, center[1] + 10, center[0] - 10, center[1] - 10);
        return;
    }

    method void drawO(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
        do Screen.setColor(true);
        do Screen.drawCircle(center[0], center[1], 10);
        do Screen.setColor(false);
        do Screen.drawCircle(center[0], center[1], 9);
        return;
    }

    method void clearSelectionIndicator(int row, int col) {
        var Array center;

        let center = getCellCenter(row, col);

        do Screen.setColor(false);
        do Screen.drawCircle(center[0], center[1], 3);

        return;
    }

    method void drawSelectionIndicator(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do Screen.setColor(true);
        do Screen.drawCircle(center[0], center[1], 3);

        return;
    }

    method void drawSelection(int player) {
        if (player = 1) {
            do drawO(playerSelection[0], playerSelection[1]);
        }
        if (player = 2) {
            do drawX(playerSelection[0], playerSelection[1]);
        }
        return;
    }

    method Array getCellCenter(int row, int col) {
        var Array center;

        let center = Array.new(2);
        let center[0] = 181 + 25 + (row * 50);
        let center[1] = 50 + 25 + (col * 50);

        return center;
    }

    method void moveLeft() {
        var int selectedCell;

        let selectedCell = getSelectedCell();
        if (~(playerSelection[0] = 0)) {
            if (~(cells[selectedCell] = -1)) {
                do drawSelection(cells[selectedCell]);
            } else {
                do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
            }
            let playerSelection[0] = playerSelection[0] - 1;
            do drawSelectionIndicator(playerSelection[0], playerSelection[1]);
        }
        return;
    }

    method void moveRight() {
        var int selectedCell;

        let selectedCell = getSelectedCell();
        if (~(playerSelection[0] = 2)) {
            if (~(cells[selectedCell] = -1)) {
                do drawSelection(cells[selectedCell]);
            } else {
                do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
            }
            let playerSelection[0] = playerSelection[0] + 1;
            do drawSelectionIndicator(playerSelection[0], playerSelection[1]);
        }
        return;
    }

    method void moveUp() {
        var int selectedCell;

        let selectedCell = getSelectedCell();
        if (~(playerSelection[1] = 0)) {
            if (~(cells[selectedCell] = -1)) {
                do drawSelection(cells[selectedCell]);
            } else {
                do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
            }
            let playerSelection[1] = playerSelection[1] - 1;
            do drawSelectionIndicator(playerSelection[0], playerSelection[1]);
        }
        return;
    }

    method void moveDown() {
        var int selectedCell;

        let selectedCell = getSelectedCell();
        if (~(playerSelection[1] = 2)) {
            if (~(cells[selectedCell] = -1)) {
                do drawSelection(cells[selectedCell]);
            } else {
                do clearSelectionIndicator(playerSelection[0], playerSelection[1]);
            }
            let playerSelection[1] = playerSelection[1] + 1;
            do drawSelectionIndicator(playerSelection[0], playerSelection[1]);
        }
        return;
    }

    method int getSelectedCell() {
        return (playerSelection[0] * 3) + playerSelection[1];
    }
}
