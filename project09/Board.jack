class Board {
    static int BOARD_LEFT;    // 181
    static int BOARD_TOP;     // 50
    static int CELL_SIZE;     // 50
    static int HALF_CELL;     // 25
    static int EMPTY_CELL;    // -1
    static int PLAYER_O;      // 1
    static int PLAYER_X;      // 2
    static int INDICATOR_R;   // 3
    static int SYMBOL_SIZE;   // 10

    field Array cells;
    field int selRow;
    field int selCol;

    function void init() {
        let BOARD_LEFT = 181;
        let BOARD_TOP = 50;
        let CELL_SIZE = 50;
        let HALF_CELL = 25;
        let EMPTY_CELL = -1;
        let PLAYER_O = 1;
        let PLAYER_X = 2;
        let INDICATOR_R = 3;
        let SYMBOL_SIZE = 10;
        return;
    }

    constructor Board new() {
        var int i;

        let cells = Array.new(9);
        let i = 0;
        while (i < 9) {
            let cells[i] = -1;
            let i = i + 1;
        }
        let selRow = 0;
        let selCol = 0;

        return this;
    }

    method void dispose() {
        do cells.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method boolean select(int player) {
        var boolean isValid;
        var int selectedCell;

        let isValid = false;
        let selectedCell = getCellIndex(selRow, selCol);
        if (cells[selectedCell] = EMPTY_CELL) {
            let isValid = true;
            let cells[selectedCell] = player;
            do drawSelection(player);
        }
        return isValid;
    }

    method void drawBoard() {
        do Screen.setColor(true);
        // Vertical lines
        do Screen.drawLine(231, 50, 231, 200);
        do Screen.drawLine(281, 50, 281, 200);
        // Horizontal lines
        do Screen.drawLine(181, 100, 331, 100);
        do Screen.drawLine(181, 150, 331, 150);
        return;
    }

    method void initSelection() {
        var int i;

        let i = 0;
        while (~(cells[i] = -1)) {
            let i = i + 1;
        }

        let selRow = i / 3;
        let selCol = i - (selRow * 3);
        do drawSelectionIndicator(selRow, selCol);
        return;
    }

    method void drawX(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do clearSelectionIndicator(selRow, selCol);
        do Screen.setColor(true);
        do Screen.drawLine(center[0] - SYMBOL_SIZE, center[1] + SYMBOL_SIZE, center[0] + SYMBOL_SIZE, center[1] - SYMBOL_SIZE);
        do Screen.drawLine(center[0] + SYMBOL_SIZE, center[1] + SYMBOL_SIZE, center[0] - SYMBOL_SIZE, center[1] - SYMBOL_SIZE);

        do center.dispose();
        return;
    }

    method void drawO(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do clearSelectionIndicator(selRow, selCol);
        do Screen.setColor(true);
        do Screen.drawCircle(center[0], center[1], SYMBOL_SIZE);
        do Screen.setColor(false);
        do Screen.drawCircle(center[0], center[1], SYMBOL_SIZE - 1);

        do center.dispose();
        return;
    }

    method void clearCurrentSelection() {
        var int selectedCell;
        let selectedCell = getCellIndex(selRow, selCol);
        if (~(cells[selectedCell] = EMPTY_CELL)) {
            do drawSelection(cells[selectedCell]);
        } else {
            do clearSelectionIndicator(selRow, selCol);
        }
        return;
    }

    method void clearSelectionIndicator(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do Screen.setColor(false);
        do Screen.drawCircle(center[0], center[1], INDICATOR_R);

        do center.dispose();
        return;
    }

    method void drawSelectionIndicator(int row, int col) {
        var Array center;
        let center = getCellCenter(row, col);

        do Screen.setColor(true);
        do Screen.drawCircle(center[0], center[1], INDICATOR_R);

        do center.dispose();
        return;
    }

    method void drawSelection(int player) {
        if (player = PLAYER_O) {
            do drawO(selRow, selCol);
        }
        if (player = PLAYER_X) {
            do drawX(selRow, selCol);
        }
        return;
    }

    method Array getCellCenter(int row, int col) {
        var Array center;
        let center = Array.new(2);
        let center[0] = BOARD_LEFT + HALF_CELL + (col * CELL_SIZE);
        let center[1] = BOARD_TOP + HALF_CELL + (row * CELL_SIZE);

        return center;
    }

    method void moveLeft() {
        if (~(selCol = 0)) {
            do clearCurrentSelection();
            let selCol = selCol - 1;
            do drawSelectionIndicator(selRow, selCol);
        }
        return;
    }

    method void moveRight() {
        if (~(selCol = 2)) {
            do clearCurrentSelection();
            let selCol = selCol + 1;
            do drawSelectionIndicator(selRow, selCol);
        }
        return;
    }

    method void moveUp() {
        if (~(selRow = 0)) {
            do clearCurrentSelection();
            let selRow = selRow - 1;
            do drawSelectionIndicator(selRow, selCol);
        }
        return;
    }

    method void moveDown() {
        if (~(selRow = 2)) {
            do clearCurrentSelection();
            let selRow = selRow + 1;
            do drawSelectionIndicator(selRow, selCol);
        }
        return;
    }

    method int getCellIndex(int row, int col) {
        return (row * 3) + col;
    }

    method boolean checkWin(int player) {
        var boolean isHorizontalWin;
        var boolean isVerticalWin;
        var boolean isDiagonalWin;

        let isHorizontalWin = checkHorizontalWin(player, selRow);
        let isVerticalWin = checkVerticalWin(player, selCol);
        let isDiagonalWin = checkDiagonalWin(player);

        return isHorizontalWin | isVerticalWin | isDiagonalWin;
    }

    method boolean checkHorizontalWin(int player, int row) {
        var boolean isWin;
        var int cellIndex;
        var int col;

        let isWin = true;
        let col = 0;
        while (col < 3) {
            let cellIndex = getCellIndex(row, col);
            if (~(cells[cellIndex] = player)) {
                let isWin = false;
            }
            let col = col + 1;
        }

        return isWin;
    }

    method boolean checkVerticalWin(int player, int col) {
        var boolean isWin;
        var int cellIndex;
        var int row;

        let isWin = true;
        let row = 0;
        while (row < 3) {
            let cellIndex = getCellIndex(row, col);
            if (~(cells[cellIndex] = player)) {
                let isWin = false;
            }
            let row = row + 1;
        }

        return isWin;
    }

    method boolean checkDiagonalWin(int player) {
        var boolean lrWin;
        var boolean rlWin;
        var int cellIndex;

        let lrWin = true;
        let cellIndex = getCellIndex(0, 0);
        if (~(cells[cellIndex] = player)) {
            let lrWin = false;
        }
        let cellIndex = getCellIndex(1, 1);
        if (~(cells[cellIndex] = player)) {
            let lrWin = false;
        }
        let cellIndex = getCellIndex(2, 2);
        if (~(cells[cellIndex] = player)) {
            let lrWin = false;
        }

        let rlWin = true;
        let cellIndex = getCellIndex(0, 2);
        if (~(cells[cellIndex] = player)) {
            let rlWin = false;
        }
        let cellIndex = getCellIndex(2, 0);
        if (~(cells[cellIndex] = player)) {
            let rlWin = false;
        }

        return (lrWin | rlWin);
    }

    method boolean checkTie() {
        var int i;

        let i = 0;
        while ((i < 9) & (~(cells[i] = -1))) {
            let i = i + 1;
        }
        return (i = 9);
    }
}
