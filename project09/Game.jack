class Game {
    field Board b;
    field int currentPlayer; // 1 = O, 2 = X
    field boolean gameFinished;

    constructor Game new() {
        let currentPlayer = 1;
        let gameFinished = false;
        let b = Board.new();

        do b.printBoard();

        return this;
    }

    method boolean getGameFinished() {
        return gameFinished;
    }

    method void nextTurn() {
        var Array playerSelection;
        var boolean turnComplete;
        var boolean validSelection;
        var boolean isWin;
        var char key;

        let isWin = false;
        let turnComplete = false;
        do b.initSelection();
        do printCurrentPlayer();

        while (~turnComplete) {
            let key = 0;
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }
            while (~(Keyboard.keyPressed() = 0)) {
                // wait for key to no longer be pressed
            }

            // Handle arrow keys
            if (key = 130) {
                do b.moveLeft();
            }
            if (key = 131) {
                do b.moveUp();
            }
            if (key = 132) {
                do b.moveRight();
            }
            if (key = 133) {
                do b.moveDown();
            }
            // Enter key, make selection, check win state, move to next turn
            if (key = 128) {
                let validSelection = b.select(currentPlayer);
                if (validSelection) {
                    let turnComplete = true;
                    let isWin = b.checkWin(currentPlayer);
                    if (isWin) {
                        let gameFinished = true;
                    } else {
                        do updatePlayer();
                    }
                }
            }
        }
        return;
    }

    method void updatePlayer() {
        if (currentPlayer = 1) {
            let currentPlayer = 2;
        } else {
            let currentPlayer = 1;
        }
        return;
    }

    method void printCurrentPlayer() {
        // Move cursor to the middle of the line for the current player text which is 18 char long
        do Output.moveCursor(20, (64 - 18) / 2);
        do Output.printString("Current Player - ");
        if (currentPlayer = 1) {
            do Output.printString("O");
        } else {
            do Output.printString("X");
        }
        return;
    }

    method void printWinner() {
        // Clear the current player text from row 20
        do Output.moveCursor(20, 0);
        do Output.printString("                                                                ");
        // Move cursor to the middle of the line for the winner text which is 40 char long
        do Output.moveCursor(20, (64 - 14) / 2);
        do Output.printString("Player ");
        if (currentPlayer = 1) {
            do Output.printString("O");
        } else {
            do Output.printString("X");
        }
        do Output.printString(" Wins!");
        return;
    }

    method String getCurrentPlayer() {
        return currentPlayer;
    }
}
