/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx & nx
    Mux16(a=x, b=false, sel=zx, out=zxout);
    Not16(in=zxout, out=notx);
    Mux16(a=zxout, b=notx, sel=nx, out=nxout);

    // zy & ny
    Mux16(a=y, b=false, sel=zy, out=zyout);
    Not16(in=zyout, out=noty);
    Mux16(a=zyout, b=noty, sel=ny, out=nyout);

    // f
    And16(a=nxout, b=nyout, out=and);
    Add16(a=nxout, b=nyout, out=add);
    Mux16(a=and, b=add, sel=f, out=outf);

    // no
    Not16(in=outf, out=notf);
    Mux16(a=outf, b=notf, sel=no, out=out);

    // zr
    Mux16(a=outf, b=notf, sel=no, out[0..7]=zrin1);
    Mux16(a=outf, b=notf, sel=no, out[8..15]=zrin2);
    Or8Way(in=zrin1, out=zror1);
    Or8Way(in=zrin2, out=zror2);
    Or(a=zror1, b=zror2, out=zror);
    Not(in=zror, out=zr);

    // ng
    Mux16(a=outf, b=notf, sel=no, out[15]=ngbit);
    And(a=ngbit, b=true, out=ng);
}
